# ZSH OPTIONS

source ~/.fzf-tab/fzf-tab.plugin.zsh
autoload -Uz compinit && compinit
autoload -U bashcompinit && bashcompinit

bindkey -e

autoload -Uz pushd
setopt auto_pushd
setopt prompt_subst
setopt share_history
setopt auto_cd

# PROMPT

autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:git:*' formats '%F{yellow}(%f%F{red}%b%f%F{yellow})%u%c%m %f'
zstyle ':vcs_info:git:*' check-for-changes true
zstyle ':vcs_info:git:*' unstagedstr '*'
zstyle ':vcs_info:git:*' stagedstr '+'
zstyle ':vcs_info:git*+set-message:*' hooks git-untracked

+vi-git-untracked() {
	if [[ $(git rev-parse --is-inside-work-tree 2>/dev/null) == 'true' ]] &&
		git status --porcelain | grep -m 1 '^??' &>/dev/null; then
		hook_com[misc]='?'
	fi
}

precmd() { vcs_info; }

PROMPT='%F{blue}%0~%f ${vcs_info_msg_0_}%F{green}%(!.#.>) %f'

# ZLE WIDGETS

jobs_widget() { echo "" && jobs && zle reset-prompt; }
fg_current_widget() { zle -I && fg %+; }
fg_previous_widget() { zle -I && fg %-; }
kill_job_current_widget() { kill %+ && fg %+; }
kill_job_previous_widget() { kill %- && fg %-; }
zle -N jobs_widget
zle -N fg_current_widget
zle -N fg_previous_widget
zle -N kill_job_current_widget
zle -N kill_job_previous_widget
for i in {1..9}; do
	eval "fg_${i}_widget() { zle -I; fg %${i}; }"
	eval "kill_job_${i}_widget() { kill %${i} && fg %${i}; }"
	eval "zle -N fg_${i}_widget"
	eval "zle -N kill_job_${i}_widget"
done

# ZSH KEYMAPS

bindkey -r '^J' # redundant -- same as ^M
bindkey '^J^J' jobs_widget
bindkey '^Jj' jobs_widget
bindkey '^I' expand-or-complete
bindkey '^]' fzf-tab-complete
bindkey '^U' backward-kill-line
bindkey '^[[Z' reverse-menu-complete
bindkey '^J^M' fg_current_widget
bindkey '^Jm' fg_current_widget
bindkey '^J^N' fg_previous_widget
bindkey '^Jn' fg_previous_widget
bindkey '^J^K^M' kill_job_current_widget
bindkey '^J^Km' kill_job_current_widget
bindkey '^J^K^N' kill_job_previous_widget
bindkey '^J^Kn' kill_job_previous_widget
for i in {1..9}; do
	eval "bindkey '^J${i}' fg_${i}_widget"
	eval "bindkey '^J^K${i}' kill_job_${i}_widget"
done

# ALIASES

alias soz="source ~/.zshrc"
alias sov="source venv/bin/activate || source .venv/bin/activate"

alias mux="cd && ~/dotfiles/tmux/tmux-session.sh"
alias nvs="nvim -S Session.vim"
alias vs="vim -S Session.vim"
alias nvcd="nvim -c 'cd %:h'"
alias nvco='nvim -c "CopilotChatOpen" -c "wincmd o" -c "startinsert"'
alias nvz="nvim -c 'cd' ~/.zshrc"
alias nvd="nvim -c 'cd %:h' ~/dotfiles"

alias k="kubectl"
alias kn="kubectl config set-context --current --namespace"
alias kx="kubectl config use-context"
alias kl="kubectl config view --minify -o json | jq -r '
	[\"NAME\", \"NAMESPACE\"],
	[.\"current-context\", .contexts[0].context.namespace // \"\"]
	| @tsv' | column -t"
alias kla='kubectl config get-contexts | sed -E "s/(^\*.*)/\x1b\[7m\1\x1b\[0m/"'

alias ga="gcloud auth login --update-adc"
alias batj="bat -l json"
alias baty="bat -l yaml"
alias jq="jq --indent 4"
alias bd=". bd -si"
alias null="/dev/null"

# FUNCTIONS

difftool() {
	nvim -c "let g:taboo_tab_format = \" %N %R \"" -c "Git difftool -y $1" \
		-c "set showtabline=2" -c "set tabclose=" -c "exe 'cd ' .. getcwd()" \
		-c "1tabcl"
}

ghclone() {
	git clone "https://github.com/$1.git"
}

kj() {
	eval "kubectl $* -o json | bat -l json"
}

ky() {
	eval "kubectl $* -o yaml | bat -l yaml"
}

# VARS

HISTSIZE=9999
SAVEHIST=9999
KEYTIMEOUT=200
# better less highlighting within tmux
export LESS_TERMCAP_so=$'\E[30;43m'
export LESS_TERMCAP_se=$'\E[39;49m'

export PATH="${KREW_ROOT:-$HOME/.krew}/bin:$PATH"
export PATH="$PATH:$HOME/.local/bin"
export EDITOR=nvim
export BINDIR="$HOME/.local/bin"
export CLICOLOR=1
export LSCOLORS=exfxcxdxbxegedabagacadah
export GIT_PAGER="diff-highlight | less"
export FZF_DEFAULT_OPTS="--bind ctrl-b:half-page-up,ctrl-f:half-page-down"

if command -v bat >/dev/null 2>&1; then
	alias less="bat"
	export BAT_STYLE=grid
	export MANPAGER=bat
fi

source <(fzf --zsh)
fpath=(~/.zsh/completion $fpath)

export TERM="xterm-256color"
[[ -n $TMUX ]] && export TERM="screen-256color"
